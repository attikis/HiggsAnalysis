#!/bin/tcsh

# ------------------  modify these values --------------
# write name of datacard here (remove .txt)
set nameDataCard = lands_datacard_hplushadronic_m
# jobs have 25 toy MC events each
set nJobs = "1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20"
# todo: massPoints not yet used everywhere 
set massPoints = "80 100 120 140 150 155 160"
# ----------------------------------------------

# do not modify these
set odir = `pwd`
set nameJobs = `basename ${odir}`
set jobname = ${nameJobs}_`date +"%H%M%S"`

# check if jobs producing same output files are running
# NOTE: a job with longer name containing the name of this job might block this 
# job, even though it would not produce output files with same name
while (`bjobs -w |& grep $nameJobs` != "") 
  echo Waiting for previous jobs to end.  You might want to bkill these jobs...
  sleep 10
end

# prepare LandS
cd mycmsswlink/src/
cvs co -r V3-04-01_eps UserCode/mschen/LandS
cd UserCode/mschen/LandS  
cmsenv
gmake clean
gmake all

cd ${odir}
mkdir outputs

rm split*.root

#Find out integrated luminosities from datacard files, one for each mass point
foreach xmas ($massPoints)
head -1 ${nameDataCard}${xmas}.txt | awk '{print strtonum(substr($7,12,5)) }' >&! outputs/input_luminosity_${xmas}
end

# jobs for observed limits, one for each mass point
foreach xmas ($massPoints)
bsub -q 1nh -J ${jobname}${xmas}_o ${odir}/mkBrLimits_submitObs ${odir} ${nameDataCard}${xmas}.txt
end

# jobs for expected limits, several jobs for each mass point 
# parameters for mkBrLimits_submitExpSplit:
# workdir, job number (seed), mass point, datacard file name(s)
foreach xmas ($massPoints)
foreach xjob ($nJobs)
bsub -q 1nh -J ${jobname}${xmas}_e_$xjob ${odir}/mkBrLimits_submitExpSplit ${odir} $xjob $xmas ${nameDataCard}${xmas}.txt
end
end

# here check if jobs with same name are running
echo Checking jobs with name $jobname.
while (`bjobs -w |& grep $jobname` != "") # check if job name exists
  echo Waiting for jobs to end `date`
  sleep 60
end

# todo : rename for mass
foreach xmas ($massPoints)
hadd split${xmas}_Combined_limits_tree.root split${xmas}_*limits_tree.root
./mycmsswlink/src/UserCode/mschen/LandS/test/lands.exe --doExpectation 1 --readLimitsFromFile split${xmas}_Combined_limits_tree.root | tail -5 >&! outputs/output_${nameDataCard}${xmas}_exp
end

# collect observed values
foreach xmas ($massPoints)
head -3 outputs/output_${nameDataCard}${xmas}_obs | tail -1 | awk '{print $12 }' >&! outputs/output_${nameDataCard}${xmas}
head -3 outputs/output_${nameDataCard}${xmas}_exp | tail -1 | awk '{print $2 " " $3 " " $7 " " $5 " " $6}' >> outputs/output_${nameDataCard}${xmas}
end

cd ${odir}
rm -r LSFJOB_*
rm *Hybrid*root

echo Ready with LSF jobs!
